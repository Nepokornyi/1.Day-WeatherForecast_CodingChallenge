<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">

    <!-- fa -->
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.0/css/all.min.css" integrity="sha512-iecdLmaskl7CVkqkXNQ/ZH/XLlvWZOJyj7Yy7tcenmpD1ypASozpmT/E0iPtmFIB46ZmdtAc9eNBvH0H/ZpiBw==" crossorigin="anonymous" referrerpolicy="no-referrer" />

    <!-- fonts -->
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Poppins:wght@300;400;700&display=swap" rel="stylesheet">

    <!-- THREE.JS -->
    <script async src="https://unpkg.com/es-module-shims@1.8.0/dist/es-module-shims.js"></script>
    <script type="importmap">
    {
        "imports": {
        "three": "https://unpkg.com/three@0.153.0/build/three.module.js",
        "three/addons/": "https://unpkg.com/three@0.153.0/examples/jsm/"
        }
    }
    </script>

    <!-- styles -->
    <link rel="stylesheet" href="style.css">

    <title>66 Day - Breathing Background</title>
</head>
<body>

    <!-- <div id="background-container"></div> -->
    <!-- <canvas id="gradientCanvas"></canvas> -->
    <div id="container">
        <script type="x-shader/x-vertex" id="vertexShader">
            varying vec2 vUv;
    
            void main() {
                vUv = uv;
                gl_Position = vec4( position, 1.0 );
            }
        </script>
        <script type="x-shader/x-fragment" id="fragmentShader">
            uniform vec2  iResolution;
            uniform float iTime;

            const mat2 m = mat2( 0.80,  0.60, -0.60,  0.80 );

            float noise( in vec2 p )
            {
              return sin(p.x)*sin(p.y);
            }
        
            float fbm4( vec2 p )
            {
                float f = 0.0;
                f += 0.5000*noise( p ); p = m*p*2.02;
                f += 0.2500*noise( p ); p = m*p*2.03;
                f += 0.1250*noise( p ); p = m*p*2.01;
                f += 0.0625*noise( p );
                return f/0.9375;
            }
        
            float fbm6( vec2 p )
            {
                float f = 0.0;
                f += 0.500000*(0.5+0.5*noise( p )); p = m*p*2.02;
                f += 0.250000*(0.5+0.5*noise( p )); p = m*p*2.03;
                f += 0.125000*(0.5+0.5*noise( p )); p = m*p*2.01;
                f += 0.062500*(0.5+0.5*noise( p )); p = m*p*2.04;
                f += 0.031250*(0.5+0.5*noise( p )); p = m*p*2.01;
                f += 0.015625*(0.5+0.5*noise( p ));
                return f/0.96875;
            }
        
            vec2 fbm4_2( vec2 p )
            {
                return vec2(fbm4(p), fbm4(p+vec2(7.8)));
            }
        
            vec2 fbm6_2( vec2 p )
            {
                return vec2(fbm6(p+vec2(16.8)), fbm6(p+vec2(11.5)));
            }

            float func( vec2 q, out vec4 ron )
            {
                q += 0.03*sin( vec2(0.27,0.23)*iTime + length(q)*vec2(4.1,4.3));
        
              vec2 o = fbm4_2( 0.9*q );
        
                o += 0.14*sin( vec2(0.12,0.14)*iTime + length(o));
        
                vec2 n = fbm6_2( 2.0*o );
        
              ron = vec4( o, n );
        
                float f = 0.5 + 0.5*fbm4( 1.8*q + 6.0*n );
        
                return mix( f, f*f*f*3.5, f*abs(n.x) );
            }

            void mainImage( out vec4 fragColor, in vec2 fragCoord )
            {
                vec2 p = (2.0*fragCoord-iResolution.xy)/iResolution.y;
                float e = 2.0/iResolution.y;
        
                vec4 on = vec4(0.0);
                float f = func(p, on);
        
              vec3 col = vec3(0.0);
                col = mix( vec3(0.0, 0.0, 0.0), vec3(0.25, 0.13, 0.02), f );
                col = mix( col, vec3(0.9,0.9,0.9), dot(on.zw,on.zw) );
                col = mix( col, vec3(0.21, 0.27, 0.56), 0.2 + 0.5*on.y*on.y );
                col = mix( col, vec3(0.9,0.9,0.9), dot(on.zw,on.zw) );
                col = clamp( col*f*2.0, 0.0, 1.0 );
        
                #if 0
                    // gpu derivatives - bad quality, but fast
                    vec3 nor = normalize( vec3( dFdx(f)*iResolution.x, 6.0, dFdy(f)*iResolution.y ) );
                #else
                    // manual derivatives - better quality, but slower
                    vec4 kk;
                    vec3 nor = normalize( vec3( func(p+vec2(e,0.0),kk)-f, 2.0*e, func(p+vec2(0.0,e),kk)-f ) );
                #endif
        
                vec3 lig = normalize( vec3( 0.9, 0.2, -0.4 ) );
                float dif = clamp( 0.3+0.7*dot( nor, lig ), 0.0, 1.0 );
                vec3 lin = vec3(0.70,0.90,0.95)*(nor.y*0.5+0.5) + vec3(0.15,0.10,0.05)*dif;
                col *= 1.2*lin;
                col = 1.0 - col;
                col = 1.1*col*col;
    
                fragColor = vec4( col, 1.0 );
            }
            void main() {
                mainImage(gl_FragColor, gl_FragCoord.xy);
            }
        </script>
    </div>
    <div class="content">
        <h1>Hello Daria!</h1>
        <h2 id="colorful-heading">OmG bEsT wEbSiTe In ThE wOrLd</h2>
        <ul>
            <li>I'm telling you</li>
            <li>this portfolio</li>
            <li>will lend you</li>
            <li>a joooob &lt;33333</li>
        </ul>
    </div>

    <script type="module" src="main.js"></script>
</body>
</html>